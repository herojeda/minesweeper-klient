plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'
    id 'application'
}

group 'org.hojeda'
version '1.0.0'

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

repositories {
    mavenCentral()
}

def jacksonVersion = "2.11.2"
def log4j2Version = "2.13.3"
def junitVersion = "5.4.0"

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Rest
    implementation("com.konghq:unirest-java:3.11.01")

    // Serializer
    implementation("org.json:json:20200518")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")

    // Log
    implementation("org.apache.logging.log4j:log4j-api:$log4j2Version")
    implementation("org.apache.logging.log4j:log4j-core:$log4j2Version")
    implementation("org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version")

    // Utils
    implementation("org.apache.commons:commons-lang3:3.8.1")

    // Test dependencies
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testImplementation("org.hamcrest:hamcrest-all:1.3")
}

test {
    useJUnitPlatform()
    reports.junitXml.destination = file("$buildDir/test-results")
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }

    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                "(${result.testCount} tests, " +
                "${result.successfulTestCount} successes, " +
                "${result.failedTestCount} failures, " +
                "${result.skippedTestCount} skipped)")
    }

}